dnl  This the autoconf script for the gimp-help-2 project.
dnl  The GPL version 2 applies to it which you may get from
dnl  http://www.gnu.org/.

AC_PREREQ(2.54)

m4_define([help_major_version], [0])
m4_define([help_minor_version], [1])

m4_define([help_version], [help_major_version.help_minor_version])

AC_INIT(gimp-help-2, [help_version])

AC_CONFIG_SRCDIR([src/gimp.xml])

AM_INIT_AUTOMAKE(no-define)


AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET


AC_PATH_PROG(XSLTPROC, xsltproc)
if test -z "$XSLTPROC"; then
  AC_MSG_ERROR([I'm sorry but I couldn't find xsltproc. For now this is the 
  only supported XSLT processor, so be sure to have it before this 
  sources will have any benefit to you.])
fi

AC_ARG_ENABLE(network, [  --enable-network        try to fetch DTDs or entities over network])

if test "x$enable_network" != "xyes"; then
  XSLTFLAGS='--nonet'
fi

AC_SUBST(XSLTFLAGS)


dnl  Default to the correct way of identifying stylesheets.
dnl  People need an XML catalog to resolve this to a local file.

STYLEBASE='http://docbook.sourceforge.net/release/xsl/current'

dnl  Allow to explicitely specify the stylesheet location
dnl  in case the XML catalog doesn't exist or is broken.

AC_ARG_WITH(xsl_stylesheets, [  --with-xsl-stylesheets=<basedir>   path to the base of the DocBook Modular Stylesheets])

if test "x$with_xsl_stylesheets" != "x"; then
  STYLEBASE=$with_xsl_stylesheets
  AC_CHECK_FILE($STYLEBASE/xhtml/docbook.xsl,,
    AC_MSG_ERROR([Couldn't find docbook.xsl in $STYLEBASE/xhtml]))
fi

AC_SUBST(STYLEBASE)


ALL_LINGUAS="de fr sv"

AC_SUBST(ALL_LINGUAS)


AC_CONFIG_FILES([
Makefile
stylesheets/plainhtml.xsl
])

AC_OUTPUT
