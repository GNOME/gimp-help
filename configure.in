dnl  This the autoconf script for the gimp-help-2 project.
dnl  The GPL version 2 applies to it which you may get from
dnl  http://www.gnu.org/.

AC_PREREQ(2.54)

m4_define([help_major_version], [0])
m4_define([help_minor_version], [1])

m4_define([help_version], [help_major_version.help_minor_version])

AC_INIT(gimp-help-2, [help_version])

AC_CONFIG_SRCDIR([src/gimp.xml])

AM_INIT_AUTOMAKE(no-define)


AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET


dnl  You can set the ALL_LINGUAS environment variable to
dnl  control what languages are build.

ALL_LINGUAS="de en fr sv"

AC_SUBST(ALL_LINGUAS)


dnl  The build of the HTML files is optional

AC_ARG_ENABLE(build,
              [  --enable-build          build the help files from XML (needs xsltproc)],, enable_build=no)

AM_CONDITIONAL(GIMP_HELP_BUILD, test "x$enable_build" = "xyes")


dnl  Search the XSLT processor

AC_PATH_PROG(XSLTPROC, xsltproc)
if test -z "$XSLTPROC" && test "x$enable_build" = "xyes"; then
  AC_MSG_ERROR([
** Couldn't find xsltproc. You will need it to build the help files.
** If you want to install the prebuilt help files only, use --disable-build.
** See the file 'INSTALL' for more help.])
fi


dnl  Optionally allow xsltproc to access DTDs over the network

AC_ARG_ENABLE(network,
              [  --enable-network        allow to fetch DTDs or entities over the network])

if test "x$enable_network" != "xyes"; then
  XSLTFLAGS='--nonet'
fi

AC_SUBST(XSLTFLAGS)


dnl  Default to the correct way of identifying stylesheets.
dnl  People need an XML catalog to resolve this to a local file.

STYLEBASE='http://docbook.sourceforge.net/release/xsl/current'

dnl  Allow to explicitely specify the stylesheet location
dnl  in case the XML catalog doesn't exist or is broken.

AC_ARG_WITH(xsl,
            [  --with-xsl=<basedir>    path to the base of the DocBook Modular Stylesheets])

if test "x$with_xsl" != "x"; then
  STYLEBASE=$with_xsl
  AC_CHECK_FILE($STYLEBASE/xhtml/docbook.xsl,,
    AC_MSG_ERROR([** Couldn't find docbook.xsl in $STYLEBASE/xhtml]))
fi

AC_SUBST(STYLEBASE)


dnl  We need the GIMP data directory to install the help files

AC_ARG_ENABLE(install,
              [  --enable-install        install the help files for GIMP (default=yes)],, enable_install=yes)

if test "x$enable_install" = "xyes"; then
  PKG_CHECK_MODULES(GIMP, gimp-2.0,, [
AC_MSG_ERROR([
** Couldn't find a GIMP-2.0 installation. If you don't want to install
** the help files, you won't need GIMP-2.0 and can use --disable-install.
** See the file 'INSTALL' for more help.])
])

  GIMP_DATADIR=`$PKG_CONFIG --variable=gimpdatadir gimp-2.0`
fi

AC_SUBST(GIMP_DATADIR)


AM_CONDITIONAL(GIMP_HELP_INSTALL, test "x$enable_install" = "xyes")


AC_CONFIG_FILES([
Makefile
stylesheets/plainhtml.xsl
])

AC_OUTPUT

AC_MSG_RESULT([
 Rebuild the help files:  $enable_build
 Install the help files:  $enable_install
 Installation prefix:     $GIMP_DATADIR
])
